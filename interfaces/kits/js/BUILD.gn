# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/multimedia/player_framework/config.gni")
import("./../../../multimedia_player_framework_aafwk.gni")

group("napi_packages") {
  deps = []
  if (multimedia_player_framework_support_jsapi) {
    deps += [
      "${multimedia_player_framework_path}/frameworks/js/ringtone_manager:systemsoundmanager",
      "//foundation/multimedia/player_framework/interfaces/kits/js:media",
      "//foundation/multimedia/player_framework/interfaces/kits/js:media_js",
    ]
  }
}

js_declaration("media_js") {
  part_name = "player_framework"
  sources = [ "./@ohos.multimedia.media.d.ts" ]
}

ohos_copy("media_declaration") {
  sources = [ "./@ohos.multimedia.media.d.ts" ]
  outputs = [ target_out_dir + "/$target_name/" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
}

ohos_shared_library("media") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "//foundation/multimedia/player_framework/interfaces/kits/js",
    "//foundation/multimedia/player_framework/frameworks/js/avplayer",
    "//foundation/multimedia/player_framework/frameworks/js/avrecorder",
    "//foundation/multimedia/player_framework/frameworks/js/player",
    "//foundation/multimedia/player_framework/frameworks/js/recorder",
    "//foundation/multimedia/player_framework/frameworks/js/media",
    "//foundation/multimedia/player_framework/frameworks/js/common",
    "//foundation/multimedia/player_framework/services/utils/include",
    "//third_party/libuv/include",
  ]

  sources = [
    "//foundation/multimedia/player_framework/frameworks/js/common/common_napi.cpp",
    "//foundation/multimedia/player_framework/frameworks/js/media/media_enum_napi.cpp",
    "//foundation/multimedia/player_framework/frameworks/js/media/native_module_ohos_media.cpp",
  ]

  if (multimedia_player_framework_support_player) {
    sources += [
      "//foundation/multimedia/player_framework/frameworks/js/player/audio_player_napi.cpp",
      "//foundation/multimedia/player_framework/frameworks/js/player/player_callback_napi.cpp",
    ]
    if (multimedia_player_framework_support_video) {
      sources += [
        "//foundation/multimedia/player_framework/frameworks/js/player/video_callback_napi.cpp",
        "//foundation/multimedia/player_framework/frameworks/js/player/video_player_napi.cpp",
      ]
    }
  }

  if (multimedia_player_framework_support_recorder) {
    sources += [
      "//foundation/multimedia/player_framework/frameworks/js/recorder/audio_recorder_napi.cpp",
      "//foundation/multimedia/player_framework/frameworks/js/recorder/recorder_callback_napi.cpp",
      "//foundation/multimedia/player_framework/frameworks/js/recorder/recorder_napi_utils.cpp",
      "//foundation/multimedia/player_framework/frameworks/js/recorder/video_recorder_napi.cpp",
    ]
  }

  if (multimedia_player_framework_support_recorder_js_api9) {
    sources += [
      "//foundation/multimedia/player_framework/frameworks/js/avrecorder/avrecorder_callback.cpp",
      "//foundation/multimedia/player_framework/frameworks/js/avrecorder/avrecorder_napi.cpp",
    ]
  }

  cflags = [
    "-std=c++17",
    "-fno-rtti",
    "-fno-exceptions",
    "-Wall",
    "-fno-common",
    "-fstack-protector-strong",
    "-Wshadow",
    "-FPIC",
    "-FS",
    "-O2",
    "-D_FORTIFY_SOURCE=2",
    "-fvisibility=hidden",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wdate-time",
    "-Werror",
    "-Wextra",
    "-Wimplicit-fallthrough",
    "-Wsign-compare",
    "-Wunused-parameter",
  ]

  deps = [
    "//foundation/multimedia/player_framework/interfaces/inner_api/native:media_client",
    "//foundation/multimedia/player_framework/services/utils:media_service_utils",
  ]

  if (multimedia_player_framework_support_player_js_api9) {
    deps += [
      "../../../../../../third_party/bounds_checking_function:libsec_shared",
      "../../../../../../foundation/multimedia/player_framework/frameworks/js/avplayer:media_avplayer",
    ]
  }

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "audio_framework:audio_client",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "napi:ace_napi",
  ]

  if (multimedia_player_framework_support_video) {
    external_deps += [ "graphic_standard:surface" ]
  }

  if (multimedia_player_framework_support_jsstack) {
    external_deps += [ "hiview:libxpower_event_js" ]
  }

  defines = []
  defines += player_framework_defines

  relative_install_dir = "module/multimedia"
  part_name = "player_framework"
  subsystem_name = "multimedia"
}
