# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("gst_plugins_common_config") {
  visibility = [ ":*" ]

  cflags = [
    "-fno-rtti",
    "-fno-exceptions",
    "-Wall",
    "-fno-common",
    "-fstack-protector-strong",
    "-FPIC",
    "-FS",
    "-O2",
    "-D_FORTIFY_SOURCE=2",
    "-fvisibility=hidden",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wdate-time",
    "-Werror",
    "-Wextra",
    "-Wimplicit-fallthrough",
    "-Wsign-compare",
    "-Wunused-parameter",
  ]

  include_dirs = [
    "include",
    "//commonlibrary/c_utils/base/include",
    "//foundation/multimedia/player_framework/services/utils/include",
    "//foundation/multimedia/player_framework/services/dfx",
    "//foundation/multimedia/player_framework/interfaces/inner_api/native",
    "//third_party/gstreamer/gstreamer",
    "//third_party/gstreamer/gstreamer/libs",
    "//third_party/gstreamer/gstplugins_base",
    "//third_party/gstreamer/gstplugins_base/gst-libs",
    "//third_party/glib/glib",
    "//third_party/glib",
    "//third_party/glib/gmodule",
  ]
}

declare_args() {
  multimedia_player_framework_no_stride = true
}

ohos_shared_library("gst_media_common") {
  install_enable = true

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
    blocklist = "../../../../../cfi_blocklist.txt"
  }

  configs = [
    ":gst_plugins_common_config",
    "//foundation/multimedia/player_framework/services/dfx:media_service_log_dfx_public_config",
  ]

  sources = [
    "buffer_type_meta.c",
    "gst_shmem_allocator.cpp",
    "gst_shmem_pool.cpp",
    "gst_shmem_wrap_allocator.cpp",
    "gst_shmemory_wrap_allocator.cpp",
    "gst_surface_allocator.cpp",
  ]

  if (!multimedia_player_framework_no_stride) {
    sources += [ "gst_producer_surface_pool.cpp" ]
  } else {
    sources += [ "gst_producer_surface_pool_no_stride.cpp" ]
  }

  deps = [
    "../../../../utils:media_service_utils",
    "//foundation/multimedia/player_framework/services/engine/gstreamer/plugins/codec/common:gst_codec_plugins_common",
    "//foundation/multimedia/player_framework/services/engine/gstreamer/plugins/sink/memsink:gst_mem_sink_base",
    "//foundation/multimedia/player_framework/services/engine/gstreamer/plugins/source/memsource:gst_mem_src_base",
    "//third_party/glib:glib",
    "//third_party/glib:gmodule",
    "//third_party/glib:gobject",
    "//third_party/gstreamer/gstreamer:gstreamer",
  ]

  external_deps = [
    "graphic_standard:surface",
    "graphic_standard:sync_fence",
    "hiviewdfx_hilog_native:libhilog",
  ]

  subsystem_name = "multimedia"
  part_name = "player_framework"
}
